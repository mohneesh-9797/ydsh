
# background job
var j = ls &
assert not $j[0]
assert not $j[1]
assert not $j.in()
assert not $j.out()
assert $j.size() == 1

var ex = 34 as Any
try { $j[2]; } catch $e { $ex = $e; }
assert $ex is OutOfRangeError

assert $j as String == "%1"
assert $j
assert $j.wait()! == 0
assert not $j


# disowned background job
$j = ls &!
assert not $j[0]
assert not $j[1]
assert not $j.in()
assert not $j.out()
assert $j.size() == 1
assert $j as String == "%0"
assert $j.wait()! == 0


# co-process
$j = coproc { read; echo reply: $REPLY; }
assert $j[0]
assert $j[1]
assert $j.in()
assert $j.out()
assert $j.size() == 1

echo hello > $j[0]
assert { read -u $j[1]; $REPLY; } == "reply: hello"

assert $j
assert $j.wait()! == 0
assert not $j
assert not $j.in()
assert not $j.out()
assert $j.size() == 1


# jobs in child
$j = {
    var j = exit 3 &
    exit ${$j.wait()!}
} &
assert $j.wait()! == 3


var j2 = true &
$j = {
 var s = $j2.wait() ?? 255;
 exit $s;
} &
assert $j.wait()! == 255
assert $j2.wait()! == 0


# signal
$j = for(;;) { true; } &
$j.suspend()
assert $j
assert not $j.wait()
assert $j

$j.resume()
assert $j
$j.kill(%'term')
assert $j.wait()! == %'term'.value() + 128


# handle SIGCONT
function handle($s : Signal) {
    echo ${$s.name()}
    exit $s
}
$SIG[%'cont'] = $handle
$j = for(;;) { true; } &
$j.suspend()
$j.resume()
assert $j.wait()! == %'cont'.value()


# detach
$j = coproc false
assert $j as String == '%1'
$j.detach()
assert $j as String == '%0'
assert $j
assert $j.wait()! == 1
