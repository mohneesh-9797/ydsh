#$test($status = 1)

#var a = ["3", "43", "d"]
var a = ["43"]
$a.insert(0, "3")
$a.insert(2, "d")
assert($a.add("hello") is Array<String>)

assert($a[0] is String)
assert($a[0] == "3")
assert($a[1] == "43")
assert($a[2] == "d")
assert($a[3] == "hello")
var ex = 34 as Any
try { $a[5]; assert($false) } catch($e) { $ex = $e }
assert($ex is OutOfRangeError)

try { $ex = 34; $a.insert(34, "1"); assert $false; } catch($e) { $ex = $e; }
assert($ex is OutOfRangeError)
try { $ex = 34; $a.insert(-1, "1"); assert $false; } catch($e) { $ex = $e; }
assert($ex is OutOfRangeError)

$a[2] = "ho"
assert($a[2] == "ho")
$ex = 34
try { $a[5] = "hoge"; assert($false) } catch($e) { $ex = $e }
assert($ex is OutOfRangeError)

assert $a.get(2)
assert ! $a.get(5)
assert $a.get(2)! == "ho"

assert($a.empty() == $false)
assert($a.size() is Int)
assert($a.size() == 4)


var b = new Array<Boolean>()

assert($b.size() == 0)
assert($b.empty() == $true)

$b.add($true)
assert($b.size() == 1)
assert($b[0])

assert($b.swap(0, $false))
assert($b[0] != $true)
$ex = 1
try { $b.swap(3, $true); assert($false) } catch($e) { $ex = $e }
assert($ex is OutOfRangeError)

$b.clear()
assert($b.empty())

# stack op
$b.push($true)
assert($b.size() == 1)
assert($b.peek())
assert($b.pop())
assert($b.empty())
$ex = 34
try { $b.pop(); assert($false) } catch($e) { $ex = $e }
assert($ex is OutOfRangeError)
$ex = 2
try { $b.peek(); assert($false) } catch($e) { $ex = $e }
assert($ex is OutOfRangeError)

$b = new [Boolean]()
$b.unshift($true)
$b.unshift($false)
assert $b.size() == 2
assert !$b.shift()
assert $b.shift()
assert $b.empty()
$ex = 100
try { $b.shift(); assert $false; } catch $e { $ex = $e; }
assert $ex is OutOfRangeError


# slice
$b = [$true, $false, $false]
var bb = $b.slice(0, $b.size())
$b.clear()
assert $bb.size() == 3 && $b.size() == 0

var ee = 34 as Any
try { $bb.slice(0, 4); assert $false; } catch $e { $ee = $e; }
assert $ee is OutOfRangeError
$ee = 1

assert $bb.from(1) as String == [$false, $false] as String
try { $bb.from(4); assert $false; } catch $e { $ee = $e; }
assert $ee is OutOfRangeError
$ee = 1

assert [0, 1, 2, 3].to(2) as String == [0, 1] as String
try {
 234567
 [0].to(-3); assert $false;
} catch $e { $ee = $e; }
assert $ee is OutOfRangeError
$ee = 1

{
    var c = [1]
    var d = [2, 3]
    $c.extend($d)
    assert $c.size() == 3
    assert $c[0] == 1
    assert $c[1] == 2
    assert $c[2] == 3

    $c.extend($c)
    assert $c.size() == 3
}


# reverse
var rr = [1, 2, 3, 4, 5]
$rr.reverse()
assert $rr.size() == 5
assert $rr[0] == 5
assert $rr[1] == 4
assert $rr[2] == 3
assert $rr[3] == 2
assert $rr[4] == 1
$rr.clear()
$rr.reverse()
assert $rr.empty()


# sort
var s1 = [4, 8, -9, -1, 78, 0, 2]
$s1.sort()
assert $s1[0] == -9
assert $s1[1] == -1
assert $s1[2] == 0
assert $s1[3] == 2
assert $s1[4] == 4
assert $s1[5] == 8
assert $s1[6] == 78

var s2 = [$false, $true, $false]
$s2.sort()
assert !$s2[0]
assert !$s2[1]
assert $s2[2]

var s3 = ["a", "AA", "1"]
$s3.sort()
assert $s3[0] == "1"
assert $s3[1] == "AA"
assert $s3[2] == "a"

var s4 = [new Int!(), 34 as Int!, new Int!()]
$s4.sort()
assert $s4[0]! == 34
assert !$s4[1]
assert !$s4[2]

var s5 = [$SIG_DFL, $SIG_IGN]
$s5.sort()  # sort result is undefined


# inheritance
assert([34, 43] is Array<Int32>)
assert([34] is Any)

assert(new Array<Any>() is Array<Any>)
